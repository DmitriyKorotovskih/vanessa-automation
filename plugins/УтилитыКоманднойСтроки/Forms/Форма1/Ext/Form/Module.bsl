
&НаКлиенте
Процедура Тест_МассивВСтроку(Команда)
	
	Для Каждого ТестКейс Из ТестКейсы_ПолучитьКоманднуюСтрокуИзМассива() Цикл
		
		Результат = ПолучитьКоманднуюСтрокуИзМассиваWindows(ТестКейс.Параметр);
		Если Результат <> ТестКейс.Результат Тогда
			ПолучитьКоманднуюСтрокуИзМассиваWindows(ТестКейс.Параметр);
			ВызватьИсключение "";	
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Тест_Запустить(Команда)

	КодВозврата = Неопределено;
	ПотокВывода = Неопределено;
	
	КомандаСистемы_("ping ya.ru",, Истина, КодВозврата, ПотокВывода);  	
	
	Результат = ПолучитьСтрокуИзДвоичныхДанных(ПотокВывода, КодировкаТекста.OEM);	
	Сообщить(Результат);
	
КонецПроцедуры

#Область КомандаСистемы

// Запускает команду в командной обочке, аналогично КомандаСистемы, реализуя при этом ожидание 
//
// Параметры:
//  СтрокаКоманды           - Строка, Массив - Командная строка для запуска приложения либо имя файла
//  ТекущийКаталог          - Строка - Задает текущий каталог запускаемого приложения
//  ДождатьсяЗавершения     - Булево - Дожидаться завершения запущенного приложения перед продолжением работы
//  КодВозврата             - Число, Неопределено - содержит код завершения работы системы
//	ДополнительныеПараметры - Структура - произвольные параметры
//
&НаКлиентеНаСервереБезКонтекста
Процедура КомандаСистемы_(СтрокаКоманды, ТекущийКаталог = "", 
	ДождатьсяЗавершения = Ложь, КодВозврата, ПотокВывода, ДополнительныеПараметры = Неопределено)
		
	СистемнаяИнформация = Новый СистемнаяИнформация;
	Если СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86
		Или СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86_64 Тогда
		
		КомандаСистемыWindows(
			СтрокаКоманды, 
			ТекущийКаталог, 
			ДождатьсяЗавершения, 
			КодВозврата, 
			ПотокВывода, 
			ДополнительныеПараметры); 
			
	Иначе
		
		КомандаСистемыPOSIX(
			СтрокаКоманды, 
			ТекущийКаталог, 
			ДождатьсяЗавершения, 
			КодВозврата, 
			ПотокВывода,
			ДополнительныеПараметры);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура КомандаСистемыPOSIX(СтрокаКоманды, ТекущийКаталог = "", 
	ДождатьсяЗавершения = Ложь, КодВозврата, ПотокВывода, ДополнительныеПараметры = Неопределено)
	
	КодВозврата = Неопределено;	
	Исполнитель = "/bin/sh";
	
	Команда = Новый Массив;
	Команда.Добавить(Исполнитель);
	Команда.Добавить("-с");
	Команда.Добавить(ПолучитьКоманднуюСтрокуИзМассиваWindows(СтрокаКоманды));

	Если ДождатьсяЗавершения Тогда
		ПотокВыводаФайл = ПолучитьИмяВременногоФайла();
		Команда.Добавить(">");
		Команда.Добавить(ПотокВыводаФайл);
	КонецЕсли;
		
	ЗапуститьПриложение(
		ПолучитьКоманднуюСтрокуИзМассиваWindows(Команда), ТекущийКаталог, ДождатьсяЗавершения, КодВозврата); 
		
	Если ДождатьсяЗавершения Тогда		
		ПотокВывода = Новый ДвоичныеДанные(ПотокВыводаФайл);
		УдалитьФайлы(ПотокВыводаФайл);
	КонецЕсли;		
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура КомандаСистемыWindows(СтрокаКоманды, ТекущийКаталог = "", 
	ДождатьсяЗавершения = Ложь, КодВозврата, ПотокВывода, ДополнительныеПараметры = Неопределено)
	
	КодВозврата = Неопределено;
	ПотокВывода = Неопределено;
	Исполнитель = "C:\WINDOWS\system32\cmd.exe";	
		
	Команда = Новый Массив;
	Команда.Добавить(Исполнитель);
	Команда.Добавить("/C");
	Команда.Добавить(ПолучитьКоманднуюСтрокуИзМассиваWindows(СтрокаКоманды));
		
	Если ДождатьсяЗавершения Тогда
		ПотокВыводаФайл = ПолучитьИмяВременногоФайла();
		Команда.Добавить(">");
		Команда.Добавить(ПотокВыводаФайл);
	КонецЕсли;
		
	ЗапуститьПриложение(
		ПолучитьКоманднуюСтрокуИзМассиваWindows(Команда), ТекущийКаталог, ДождатьсяЗавершения, КодВозврата); 
		
	Если ДождатьсяЗавершения Тогда		
		ПотокВывода = Новый ДвоичныеДанные(ПотокВыводаФайл);
		УдалитьФайлы(ПотокВыводаФайл);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Служебные

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьКоманднуюСтрокуИзМассиваWindows(Значение)
	
	Если ТипЗнч(Значение) = Тип("Строка") Тогда
		Возврат Значение;
	КонецЕсли;
	
	Если ТипЗнч(Значение) <> Тип("Массив") Тогда
		ВызватьИсключение "ОшибкаЗначения: Не предполагаемое значение параметра №1";
	КонецЕсли;
	
	Пробел = " ";
	Бекслеш = "\";
	Кавычка = """";
	
	Результат = Новый Массив;
	НужныКавычки = Ложь;
	
	Для Каждого Аргумент Из Значение Цикл
		
		БекслешМассив = Новый Массив;
		
		Если Результат.Количество() Тогда
			Результат.Добавить(Пробел);	
		КонецЕсли;
		
		НужныКавычки = Найти(Аргумент, Пробел) 
			Или Найти(Аргумент, Символы.Таб) 
			Или НЕ ЗначениеЗаполнено(Аргумент);
		
		Если НужныКавычки Тогда
			Результат.Добавить(Кавычка);		
		КонецЕсли;		
				
		Для Сч = 1 По СтрДлина(Аргумент) Цикл
			
			Символ = Сред(Аргумент, Сч, 1);
			Если Символ = Бекслеш Тогда
				БекслешМассив.Добавить(Символ);
			ИначеЕсли Символ = Кавычка Тогда
				БекслешДвойной = 2;
				БекслешСтрока = "";
				Пока СтрДлина(БекслешСтрока) <> СтрДлина(Бекслеш) * БекслешМассив.Количество() * БекслешДвойной Цикл
					БекслешСтрока = БекслешСтрока + Бекслеш;	
				КонецЦикла;
				БекслешМассив = Новый Массив;
				Результат.Добавить(БекслешСтрока);
				Результат.Добавить(Бекслеш + Кавычка);
			Иначе
				Для Каждого Элемент Из БекслешМассив Цикл
					Результат.Добавить(Элемент);	
				КонецЦикла;
				БекслешМассив = Новый Массив;
				Результат.Добавить(Символ);
			КонецЕсли;	
			
		КонецЦикла;	
		
		Для Каждого Элемент Из БекслешМассив Цикл
			Результат.Добавить(Элемент);	
		КонецЦикла;
		
		Если НужныКавычки Тогда
			Для Каждого Элемент Из БекслешМассив Цикл
				Результат.Добавить(Элемент);	
			КонецЦикла;
		    Результат.Добавить(Кавычка);
		КонецЕсли;			
		
	КонецЦикла;
	
	Возврат СтрСоединить(Результат);
	
КонецФункции

#КонецОбласти

#Область ТестКейсы

&НаКлиенте
Функция ТестКейсы_ПолучитьКоманднуюСтрокуИзМассива()

	ТестКейсы = Новый Массив;
	ТестКейсы.Добавить(ТестКейс1_ПолучитьКоманднуюСтрокуИзМассива());
	ТестКейсы.Добавить(ТестКейс2_ПолучитьКоманднуюСтрокуИзМассива());
	ТестКейсы.Добавить(ТестКейс3_ПолучитьКоманднуюСтрокуИзМассива());
	ТестКейсы.Добавить(ТестКейс4_ПолучитьКоманднуюСтрокуИзМассива());
	
	Возврат ТестКейсы;
	
КонецФункции

&НаКлиенте
Функция ТестКейс1_ПолучитьКоманднуюСтрокуИзМассива()
	
	Массив = Новый Массив;
	Массив.Добавить("abc");
	Массив.Добавить("d");
	Массив.Добавить("e");
	
	Возврат Новый Структура("Параметр, Результат", Массив, "abc d e");
	
КонецФункции

&НаКлиенте
Функция ТестКейс2_ПолучитьКоманднуюСтрокуИзМассива()
	
	Массив = Новый Массив;
	Массив.Добавить("a\\b");
	Массив.Добавить("de fg");
	Массив.Добавить("h");
	
	Возврат Новый Структура("Параметр, Результат", Массив, "a\\b ""de fg"" h");
	
КонецФункции

&НаКлиенте
Функция ТестКейс3_ПолучитьКоманднуюСтрокуИзМассива()
	
	Массив = Новый Массив;
	Массив.Добавить("a""b");
	Массив.Добавить("c");
	Массив.Добавить("d");
	
	
	Возврат Новый Структура("Параметр, Результат", Массив, "a\""b c d");
	
КонецФункции

&НаКлиенте
Функция ТестКейс4_ПолучитьКоманднуюСтрокуИзМассива()
	
	Массив = Новый Массив;
	Массив.Добавить("a\\b c");
	Массив.Добавить("d");
	Массив.Добавить("e");
	
	Возврат Новый Структура("Параметр, Результат", Массив, """a\\b c"" d e");
	
КонецФункции

#КонецОбласти